#include <LiquidCrystal_PCF8574.h>
#include <AccelStepper.h>
#include <esp_sleep.h> // might change back

// Pin Definitions
#define encoderCLK 4
#define encoderDT 5
#define rotaryButton 18
// #define enablePin 12   // Enable pin  FOR TREADMILL (optional)
#define treadDirectionPin 25
#define treadPulsePin 27

#define augerDirectionPin 14
#define augerPulsePin 13

#define auger2DirectionPin 32
#define auger2PulsePin 33

#define harnessPulsePin 15
#define harnessDirectionPin 16

#define Button 23 // 19
#define emergencyButton 26
#define clockwiseButton 17 //23
#define counterclockwiseButton 19  // 17

// LCD Settings
const int lcdColumns = 16, lcdRows = 2;
LiquidCrystal_PCF8574 lcd(0x27);

// Treadmill Settings
AccelStepper treadmill(1, treadPulsePin, treadDirectionPin);
int treadmillSpeed = 0;                   // Initial Treadmill Speed
const int maxSpeed = 1337;                // Max Treadmill Speed
const int treadmillIncrement = 17;        // Speed Adjustment Step

// Auger Settings
AccelStepper auger(1, augerPulsePin, augerDirectionPin);
int augerSpeed = 0;                       // Initial Auger Speed
const int augerMaxSpeed = 2044;           // Max Auger Speed
const int augerIncrement = 26;            // Speed Adjustment Step

// 2nd Auger (Top Auger)
AccelStepper auger2(1, auger2PulsePin, auger2DirectionPin);
int auger2Speed = 0;                       // Initial Auger Speed
const int auger2MaxSpeed = 2044;           // Max Auger Speed
const int auger2Increment = 26;            // Speed Adjustment Step

// Harness Motor Settings
AccelStepper harness(1, harnessPulsePin, harnessDirectionPin); // Harness motor
bool positionSaved = false;
int setPosition = 0; // Saved position for harness adjustments
bool eButtonPressed = false;
unsigned long lastMoveTime = 0;  // To track the last movement time
int count = 0;             // Initialize the count for rotary encoder steps

// Menu Variables
enum Mode { MENU, SPEED_MODE, HARNESS_MODE } currentMode = MENU;
const String menuItems[] = {"SPEED", "HARNESS"};
int menuIndex = 0;
const int numMenuItems = 2;

// Rotary Encoder State
int lastCLKState = LOW;
unsigned long lastDebounceTime = 0;
const unsigned long debounceDelay = 50;

// Button Release Guard
bool buttonReleaseGuard = false;
unsigned long buttonReleaseTime = 0;
const unsigned long buttonReleaseDelay = 200; // Ignore button presses for 200ms

int buttonState = 0;
int eButtonState = 0;

void setup() {
  Serial.begin(115200);

  // Initialize LCD
  lcd.begin(lcdColumns, lcdRows);
  lcd.setBacklight(255);
  displayMenu();

  // Initialize Rotary Encoder Pins
  pinMode(encoderCLK, INPUT);
  pinMode(encoderDT, INPUT);
  pinMode(rotaryButton, INPUT_PULLUP);
  pinMode(Button, INPUT_PULLUP);
  pinMode(emergencyButton, INPUT_PULLUP);
  pinMode(clockwiseButton, INPUT_PULLUP);
  pinMode(counterclockwiseButton, INPUT_PULLUP);

  // Treadmill Setup
  treadmill.setMaxSpeed(maxSpeed);
  treadmill.setAcceleration(2000);

  // Auger Setup
  auger.setMaxSpeed(augerMaxSpeed);
  auger.setAcceleration(2000);

  // 2nd Auger Setup
  auger2.setMaxSpeed(augerMaxSpeed);
  auger2.setAcceleration(2000);

  // Harness Motor Setup
  harness.setMaxSpeed(800);         // Adjust max speed for smooth operation
  harness.setAcceleration(300);    // Set acceleration for smoother transitions
  harness.setSpeed(0);             // Initially, set the motor speed to 0

  lastCLKState = digitalRead(encoderCLK);
}

void loop() {
  static Mode previousMode = MENU; // Keep track of the previous mode

  if (currentMode == MENU) {
    // Guard against lingering button presses after returning to MENU
    if (buttonReleaseGuard && millis() - buttonReleaseTime > buttonReleaseDelay) {
      buttonReleaseGuard = false; // Clear the guard after the delay
    }

    if (!buttonReleaseGuard) {
      // Refresh menu display when transitioning to MENU mode
      if (previousMode != MENU) {
        lcd.clear();
        displayMenu();
        previousMode = MENU; // Update the mode tracker
      }
      handleEncoderForMenu(); // Handle rotary encoder navigation
      if (buttonPressed(rotaryButton)) handleMenuSelection(); // Select menu item
    }

    // Keep the treadmill running at the set speed
    if (treadmillSpeed > 0) {
      treadmill.runSpeed(); // Ensure treadmill continues to run
      auger.runSpeed();
      auger2.runSpeed();

    }
  } else if (currentMode == SPEED_MODE) {
    handleSpeedMode();
    if (treadmillSpeed && augerSpeed == 0 && auger2Speed == 0){
    if (buttonPressed(rotaryButton)) returnToMenu(); // Return to menu if button is pressed
    else{}
  }
  } else if (currentMode == HARNESS_MODE) {
    handleHarnessMode();
    if (buttonPressed(rotaryButton)) returnToMenu(); // Return to menu if button is pressed
  }
}

// Function to Handle Rotary Encoder for Menu Navigation
void handleEncoderForMenu() {
  int currentCLKState = digitalRead(encoderCLK);
  if ((millis() - lastDebounceTime) > debounceDelay && currentCLKState != lastCLKState) {
    if (digitalRead(encoderDT) != currentCLKState) {
      menuIndex++; // Clockwise rotation
    } else {
      menuIndex--; // Counter-clockwise rotation
    }
    menuIndex = constrain(menuIndex, 0, numMenuItems - 1); // Ensure menuIndex stays within bounds
    displayMenu(); // Update LCD display
    lastCLKState = currentCLKState;
    lastDebounceTime = millis();
  }
}

// Function to Handle Menu Item Selection
void handleMenuSelection() {
  lcd.clear();
  lcd.print(menuItems[menuIndex] + " SELECTED");
  Serial.println(menuItems[menuIndex] + " SELECTED");
  delay(1000); // Simple delay to display selection

  if (menuIndex == 0) {
    currentMode = SPEED_MODE; // Switch to SPEED_MODE
    lcd.clear();
    lcd.print("Speed Mode");
    treadmill.setSpeed(treadmillSpeed); // Maintain the current treadmill speed
  } else if (menuIndex == 1) {
    currentMode = HARNESS_MODE; // Switch to HARNESS_MODE
    lcd.clear();
    lcd.print("Harness Mode");
  }
}

// Function to Display Menu on LCD
void displayMenu() {
  lcd.clear();             // Ensure the display is fully cleared
  lcd.setCursor(0, 0);     // Reset the cursor position
  lcd.print("MENU");       // Display the menu header
  lcd.setCursor(0, 1);     // Move to the second line
  lcd.print(menuIndex == 0 ? "-> SPEED" : "-> HARNESS"); // Display the menu item
}

// Function to Handle Button Press Debouncing
bool buttonPressed(int pin) {
  if (digitalRead(pin) == LOW && (millis() - lastDebounceTime) > debounceDelay) {
    lastDebounceTime = millis();
    return true;
  }
  return false;
}

void updateLCD(){
    // Calculate RPM and linear speed
    float rpm = (abs(treadmillSpeed) * 60.0) / 400.0; // Assuming 400 steps per revolution
    float linearSpeed = (rpm * 0.0762 * PI) / 60.0;   // Convert RPM to m/s
    // Update the LCD display
    lcd.setCursor(0, 1);
    lcd.print("Speed: ");
    lcd.print(linearSpeed, 2); // Print linear speed with 2 decimal places
    lcd.print(" m/s    ");     // Clear remaining characters on the line
}

// Speed Mode Logic
void handleSpeedMode() {
  int currentCLKState = digitalRead(encoderCLK);
  int currentDTState = digitalRead(encoderDT);
  // If the rotary encoder has been turned
   if (currentCLKState != lastCLKState) {
    // Clockwise rotation (increase speed)
    if (digitalRead(encoderDT) != currentCLKState) {
      treadmillSpeed += treadmillIncrement; // Increase speed by finer increment
      augerSpeed += augerIncrement;
      auger2Speed += auger2Increment;
     // updateLCD();
    } 
    // Counterclockwise rotation (decrease speed)
    else {
      treadmillSpeed -= treadmillIncrement; // Decrease speed by finer increment
      augerSpeed -= augerIncrement;
      auger2Speed -= auger2Increment;
     // updateLCD();
    }
    // Constrain the speed within valid range
    treadmillSpeed = constrain(treadmillSpeed, 0, maxSpeed);
    augerSpeed = constrain(augerSpeed, 0, augerMaxSpeed);
    auger2Speed = constrain(auger2Speed, 0, auger2MaxSpeed);

    // Update the motor speed
    treadmill.setSpeed(-treadmillSpeed);
    auger.setSpeed(augerSpeed);
    auger2.setSpeed(auger2Speed);

    // // Calculate RPM and linear speed
    // float rpm = (abs(treadmillSpeed) * 60.0) / 400.0; // Assuming 400 steps per revolution
    // float linearSpeed = (rpm * 0.0762 * PI) / 60.0;   // Convert RPM to m/s

  // Update the last CLK state
    lastCLKState = currentCLKState;

    // // Update the LCD display
    // lcd.setCursor(0, 1);
    // lcd.print("Speed: ");
    // lcd.print(linearSpeed, 2); // Print linear speed with 2 decimal places
    // lcd.print(" m/s    ");     // Clear remaining characters on the line

  
  }

  // Run the treadmill at the updated speed
  treadmill.runSpeed();
  auger.runSpeed();
  auger2.runSpeed();

  
  
  eButtonState = digitalRead(emergencyButton);

  if (eButtonState == HIGH) {
    eButtonPressed = false;
  }
    if (eButtonState == HIGH ) {
    eStopProcedure();
    }
    
    if (treadmillSpeed == 0 && augerSpeed == 0 && auger2Speed == 0) {
        if (buttonPressed(rotaryButton)) {  // Change to your actual button variable if needed
            returnToMenu();  //  Return to menu when button is pressed
        }
    }

}

// Need to Fix
void eStopProcedure() {
    eButtonPressed = true;

    // Start harness movement *without blocking execution*
    harness.setMaxSpeed(800);
    harness.setAcceleration(300);

    if (positionSaved) {
        harness.moveTo(setPosition);
        harness.runToPosition();

    } else {
        harness.moveTo(harness.currentPosition() - 200);
    }

    // Keep treadmill & auger running during stepper movement
    treadmill.setAcceleration(300);
    auger.setAcceleration(300);
    auger2.setAcceleration(300);

    int currentTreadmillSpeed = treadmillSpeed;
    int currentAugerSpeed = augerSpeed;
    int currentAuger2Speed = auger2Speed;

    treadmill.setSpeed(-currentTreadmillSpeed);
    auger.setSpeed(currentAugerSpeed);
    auger2.setSpeed(currentAuger2Speed);

    // Timing-based gradual deceleration while stepper is moving
    unsigned long lastUpdate = millis();
    int decelStep = 10;
    int updateInterval = 30;
    int augDec = 20;

    while ((currentTreadmillSpeed > 0 || currentAugerSpeed > 0 || currentAuger2Speed > 0) || harness.distanceToGo() != 0) {
        if (millis() - lastUpdate >= updateInterval) {
            lastUpdate = millis();
            
            if (currentTreadmillSpeed > 0) {
                currentTreadmillSpeed = max(0, currentTreadmillSpeed - decelStep);
                treadmill.setSpeed(-currentTreadmillSpeed);
            }
            if (currentAugerSpeed > 0) {
                currentAugerSpeed = max(0, currentAugerSpeed - augDec);
                auger.setSpeed(currentAugerSpeed);
            }
            if (currentAuger2Speed > 0) {
                currentAuger2Speed = max(0, currentAuger2Speed - augDec);
                auger2.setSpeed(currentAuger2Speed);
            }
        }

        // Keep all motors running while decelerating & moving stepper
        treadmill.runSpeed();
        auger.runSpeed();
        auger2.runSpeed();
        harness.run();  // **Non-blocking stepper movement**
    }

    // Ensure all motors fully stop
    treadmill.setSpeed(0);
    auger.setSpeed(0);
    auger2.setSpeed(0);
    treadmill.runSpeed();
    auger.runSpeed();
    auger2.runSpeed();

    harness.moveTo(harness.currentPosition() + 200);
    while (harness.distanceToGo() != 0) {
        harness.run();
    }

    // Ensure motors fully stop at the end
    // treadmill.setSpeed(0);
    // auger.setSpeed(0);
    // treadmill.runSpeed();
    // auger.runSpeed();

    Serial.println("EMERGENCY STOP COMPLETE - ENTERING LOW POWER MODE");
    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print("SYSTEM SHUTDOWN");
    lcd.setCursor(0, 1);
    lcd.print("RESTART REQUIRED");
    
    delay(5000);
    esp_sleep_enable_ext0_wakeup(GPIO_NUM_0, 0);      // Enables Sleep Mode for ESP32
    esp_deep_sleep_start();
  
}

// Harness Mode Logic
void handleHarnessMode() {
  buttonState = digitalRead(Button);
  if (buttonState == LOW) {
    delay(200);
    lcd.setCursor(0, 1);
    lcd.print("Position Saved");  
    delay(1000);
    lcd.setCursor(0, 1);
    lcd.print("               ");  

    setPosition = harness.currentPosition();
    positionSaved = true;
  }

  // Emergency Button Logic
  eButtonState = digitalRead(emergencyButton);


  if (eButtonState == HIGH) {
    eButtonPressed = false;
  }
   if (digitalRead(clockwiseButton) == LOW) {
    harness.setSpeed(200);  // Clockwise rotation at 200 pulses
    harness.runSpeed();
  } else if (digitalRead(counterclockwiseButton) == LOW) {
    harness.setSpeed(-200);  // Counterclockwise rotation at 200 pulses
    harness.runSpeed();
  } else {
    harness.setSpeed(0);  // Stop the motor if no button is pressed
  }

  harness.runSpeed();
}

// Function to Return to Menu
void returnToMenu() {
  currentMode = MENU;                // Switch back to MENU mode
  buttonReleaseGuard = true;         // Set guard flag to prevent immediate selection
  buttonReleaseTime = millis();      // Record the current time

  positionSaved = false;             // Reset saved position flag
  eButtonPressed = false;            // Reset emergency button state

  lcd.clear();                       // Clear the LCD before refreshing the menu
  displayMenu();                     // Refresh the menu display
  Serial.println("Returned to MENU");
}
